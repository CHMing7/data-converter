apply from: 'dependency.gradle'


version = '1.0.0'
group = 'io.gitee.chming7'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    version = rootProject.version
    group = rootProject.group

    repositories {
        maven { url = "http://maven.aliyun.com/nexus/content/repositories/jcenter" }
        maven { url = "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
        mavenLocal()
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    publishing {

        publications {
            maven(MavenPublication) {
                groupId = rootProject.group
                version = rootProject.version
                from components.java
                // 主要是这一行
                artifact sourceJar
                artifact javadocJar
                pom {
                    // artifactName
                    name = 'data-converter'
                    // 构件描述
                    description = '数据转换工具库'
                    // 构件主页
                    url = 'https://gitee.com/chming7/data-converter'
                    // 许可证名称和地址
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://mit-license.org'
                        }
                    }
                    // 开发者信息
                    developers {
                        developer {
                            name = 'CHMing'
                            email = '654849388@qq.com'
                        }
                    }
                    // 版本控制仓库地址
                    scm {
                        url = 'https://gitee.com/chming7/data-converter'
                        connection = 'scm:git:https://gitee.com/chming7/data-converter.git'
                        developerConnection = 'scm:git:https://gitee.com/chming7/data-converter.git'
                    }
                }
            }
        }
        // 定义发布到那里
        repositories {
            maven {
                name 'mavenCentral'
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.properties.sonatypeUsername
                    username = project.properties.sonatypePassword
                }
            }
        }

        signing {
            sign publishing.publications.maven
        }

        javadoc {
            options.encoding = "UTF-8"
            failOnError = false
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            // Show that tests are run in the command-line output
            events 'started', 'passed'
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}
